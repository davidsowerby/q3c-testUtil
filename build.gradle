import org.ajoberstar.grgit.Commit
import org.ajoberstar.grgit.Grgit
import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

/*
 *
 *  * Copyright (c) 2016. David Sowerby
 *  *
 *  * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 *  * the License. You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 *  *
 *  * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 *  * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *  * specific language governing permissions and limitations under the License.
 *
 */


buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'commons-io:commons-io:2.4'
        classpath 'uk.q3c:q3c-gradle:0.7.4'
    }
}

plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'org.sonarqube' version "1.2"
    id 'com.github.kt3k.coveralls' version '2.4.0x'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
    id 'org.ajoberstar.grgit' version '1.4.2'
    id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.21'
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'q3c-release'
apply from: 'gradle/versioning.gradle'
apply from: 'gradle/jacocogate.gradle'

description = 'Generic test utilities'
group = 'uk.q3c'
version = '0.9.0.140'
ext.majorMinorVersion = '0.8'


ext.assertjVersion = '3.3.0'
ext.commonsLangVersion = '3.3.2'
ext.groovyVersion = '2.4.1'
ext.guavaVersion = '19.0'
ext.slf4jVersion = '1.7.6'


ext.versionDescription = 'No version description provided'
ext.releaseDate = new Date()

sourceCompatibility = '1.8'



repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
//	compile 'uk.q3c.krail:krail:0.9.8'

    //logging
    compile 'org.slf4j:slf4j-api:' + slf4jVersion
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'ch.qos.logback:logback-core:1.1.2'
    //groovy
    compile 'org.codehaus.groovy:groovy-all:' + groovyVersion

    //utility
    compile 'com.google.guava:guava:' + guavaVersion
    compile 'org.apache.commons:commons-lang3:' + commonsLangVersion
    compile 'commons-io:commons-io:2.4'

    //Testing
    compile 'com.google.code.findbugs:annotations:3.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:' + assertjVersion
    testCompile 'org.assertj:assertj-guava:1.3.1'
    testCompile 'com.mycila.testing.plugins:mycila-testing-mockito:2.8'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.0' // needed for Spock mocking

}

testSets {
    integrationTest
}

//bintray {
//    dryRun = false
//}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

task printVersion() {
    println version
}

task createBuildInfoFile << {
    def buildInfoFile = new File("$buildDir/build-info.properties")
    String timestamp = LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyyMMddHHmmss"))
    def Grgit repo = Grgit.open(project.file('.'))
    def Commit commit = repo.head()
    Properties properties = new Properties()
    properties.setProperty('version', project.version.toString())
    properties.setProperty('timestamp', timestamp)
    properties.setProperty('githash', commit.getId())
    properties.store(buildInfoFile.newWriter(), null)
}


jar {
    dependsOn createBuildInfoFile
    from(buildDir) {
        include 'build-info.properties'
        into 'META-INF'
    }
}

jacocoTestReport.reports.xml.enabled = true


publishing {
    publications {
        mavenStuff(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

javadoc {
    failOnError = false
}



bintray {
    user = 'dsowerby'
    key = getKeyForBintray()

    publications = ['mavenStuff'] //When uploading Maven-based publication files
    dryRun = false  //project.ext.bintrayDryRun //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
//        userOrg = 'myorg' //An optional organization name when the repo belongs to one of the user's orgs
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/davidsowerby/' + project.name
        issueTrackerUrl = websiteUrl + '/issues'
        vcsUrl = websiteUrl + '.git'
        licenses = ['Apache-2.0']
        labels = ['test']
        publicDownloadNumbers = true
        //Optional version descriptor
        version {
            name = project.version
            desc = project.ext.versionDescription
            released = project.ext.releaseDate
            //'optional, date of the version release' //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
            }
        }
    }
}

task gitChangelog(type: GitChangelogTask) {
    filePath = "CHANGELOG.md";
    templateContent = new File('changelog.mustache').getText('UTF-8');
}

//Need to identify whether platform has the Bintray key (Travis etc will not)
def getKeyForBintray() {
    def keyValue = System.getenv("BINTRAY")
    if (keyValue == null) {
        println 'returning no bintray key'
        return 'no bintray key'
    } else {
        println 'returning bintray key: '
        return bintrayKey
    }
}



